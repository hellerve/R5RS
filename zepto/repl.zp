; A small REPL for zepto in zepto
(load "readline")
(import-all "readline" "rl")

(define *prompt* "zepto> ")
(define *prompt2* ">>> ")
(define *input-highlight* :reset)
(define *output-highlight* :reset)
(define *syntax-highlight* #f)
(define *ret* "=> ")
(define *statement-count* 1)
(define *local-conf* "./.zeptorc")
(define *conf* (++ (os:get-home-dir) "/.zeptorc"))
(define *histfile* (++ (os:get-home-dir) "/.zepto_history"))
(define *env* *global-env*)
(define *header* (++ "zepto Version " zepto:version-str ", compiled with GHC version " zepto:ghc-str "
Copyright (C) 2016 Veit Heller (GPL)
This is free software; see the accompanying LICENSE for copying conditions.
There is NO warranty whatsoever.
Hail Eris, all rites reversed.

Type ':quit' or press Ctrl-C to exit interpreter,
':help' to get a list of commands or ':license' to get the license text"))
(define *record* #{:record? #f :file #f})

(define (write-plain arg)
  "do not expose"
  (begin
    (color :reset)
    (write arg)))

(define (error-plain . args)
  "do not expose"
  (begin
    (color :reset)
    (apply error args)))

(define (do-exit . args)
  "do not expose"
  (begin
    (write-plain "Moriturus te saluto!")
    (if (*record* :record?) (close-output-file (*record* :file)))
    (exit (if (null? args) 0 (string->number (car args))))))

(define (show-commands . args)
  "do not expose"
  (begin
    (map (lambda (x) (write-plain (++ (car x) ": " (->string (cadr x)))))
         (zepto:get-bindings (if (null? args) "" (car args)) *env*))
    (repl)))

(define (set-prompt . args)
  "do not expose"
  (if (not (null? args))
    (begin
     (set! *prompt* (string:join args))
     (repl))
    (begin
      (error-plain "option :p requires an argument (example: ':p=new-prompt>')")
      (repl))))

(define (show-license . args)
  "do not expose"
  (begin
    (write-plain (read-contents (++ (zepto:home) "assets/license_interactive.as")))
    (repl)))

(define (show-complete-license . args)
  "do not expose"
  (begin
    (write-plain (read-contents (++ (zepto:home) "assets/complete_license.as")))
    (repl)))

(define (record . args)
  "do not expose"
  (if (not (null? args))
    (begin
     (set! *record* (make-hash :record? #t :file (open-output-file (car args))))
     (write-plain (++ "Recording session to " (car args)))
     (repl))
    (begin
      (error-plain "option :record-to requires an argument (example: ':record-to=output')")
      (repl))))

(define (stop-record . args)
  "do not expose"
  (if (*record* :record?)
    (begin
      (close-output-file (*record* :file))
      (set! *record* #{:record? #f :file #f})
      (repl))
    (begin
      (error-plain "recording not active")
      (repl))))

(define (get-ddate . args)
  "do not expose"
  (begin
    (load "datetime/datetime")
    (write (++ "It's " ((import "datetime:ddate-from-timestamp") (car (unix-timestamp)))))
    (repl)))

(define (time . args)
  "do not expose"
  (define (sec-fmt nsecs)
    (cond
      ((> nsecs 10000000) (++ (->string (/ nsecs 1000000000.0)) "s"))
      ((> nsecs 100000) (++ (->string (/ nsecs 1000000.0)) "ms"))
      ((> nsecs 1000) (++ (->string (/ nsecs 1000.0)) "Âµs"))
      (else (++ (->string nsecs) "ns"))))

  (if (null? args)
    (begin
      (error-plain "option :time requires an argument")
      (repl))
    (let ((parsed (string:parse (car args))))
      (begin
        (load "bench/bench")
        (let ((res (catch-vm-error `(,(import "bench:time") ,parsed))))
          (if (number? res)
            (write (sec-fmt ((import "bench:time") parsed)))
            (write (++ "Caught error:\n\n" res))))
        (repl)))))

(define (get-type . args)
  "do not expose"
  (if (null? args)
    (begin
      (error-plain "option :type requires an argument")
      (repl))
    (let ((parsed (string:parse (car args))))
      (begin
        (write-plain (catch-vm-error [typeof (catch-error parsed)]))
        (repl)))))

(define meta-commands
  (make-hash
    ":q" do-exit
    ":record-to" record
    ":stop-recording" stop-record
    ":quit" do-exit
    ":p" set-prompt
    ":license" show-license
    ":ddate" get-ddate
    ":complete-license" show-complete-license
    ":help" show-commands
    ":type" get-type
    ":time" time
    ":prompt" set-prompt))

(define (write-hist-item item) "do not expose"
  (let* ((cont (read-contents *histfile*))
         (hist-to (open-output-file *histfile*)))
    (begin
      (rl:add-history item)
      (write (substring cont 0 (sub1 (length cont))) hist-to)
      (write item hist-to)
      (close-output-file hist-to))))

(define (string:parseable? str) "do not expose"
  (not (in? (->string (catch-vm-error `(string:parse ,str)))
            "Parse error at")))

(define (enrich input str) "do not expose"
  (let loop ((split (string:split str #\newline)))
    (if (in? (car split) "Parse error at")
      (let ((col (string->number (car (string:split (cadr (string:split (car split) "column ")) #\))))))
        (++ (car split) "\n"
            "  " input "\n"
            (color :yellow #{:to-str #t})
            "  " (string:replicate (sub1 col) " ") "^\n"
            (color :reset #{:to-str #t})
            (string:join (cdr split) #\newline)))
      (loop (cdr split)))))

(define (repl . env)
  "starts a zepto REPL. Optionally takes an environment in which to run.

   params:
    - env: optional environment in which the REPL should run
   complexity: O(1)
   returns: this function terminates the program on exit"
  (define (check acc)
    (define (get-secondary)
      (rl:readline (++ *prompt2* (color *input-highlight* #{:to-str #t})) *syntax-highlight*))
    (if (string:parseable? acc)
      (make-hash :output (catch-vm-error (macro-expand (string:parse acc)) *env*)
                 :input acc)
      (if (> (string:count acc #\() (string:count acc #\)))
        (check (++ acc " " (get-secondary)))
        (make-hash :output (enrich acc (->string (catch-vm-error `(string:parse ,acc))))
                   :input acc))))
  (begin
    (if (truthy? env) (set! *env* (car env)))
    (define input (rl:readline (++ (if (function? *prompt*) (*prompt*) *prompt*)
                                   (color *input-highlight* #{:to-str #t}))
                               *syntax-highlight*))
    (color :reset)
    (if (falsy? input)
      (repl))
    (set! *statement-count* (add1 *statement-count*))
    (define checked (check input))
    (write-hist-item (checked :input))
    (define res (->string (checked :output)))
    (let* ((split (string:split (checked :input) #\=))
           (cmd   (car split)))
      (if (not (nil? (meta-commands cmd)))
        (apply (meta-commands cmd) (cdr split))
        (begin
          (display (if (function? *ret*) (*ret*) *ret*))
          (color *output-highlight*)
          (write res)
          (rl:reset-completions (hash:keys (env->hashmap *env*)))
          (if (*record* :record?)
            (begin
              (display input (*record* :file))
              (display " ; " (*record* :file))
              (write res (*record* :file))))
          (repl))))))

(if (not (eq? (open-input-file *local-conf*) #f))
  (load *local-conf*))

(if (not (eq? (open-input-file *conf*) #f))
  (load *conf*))

(let ((x (open-input-file *histfile*)))
  (if (not (eq? x #f))
    (begin
      (rl:set-history! (reverse (string:split (read-contents *histfile*) #\newline)))
      (close-input-file x))))

(rl:add-completions (hash:keys (env->hashmap *env*)))

(write *header*)
